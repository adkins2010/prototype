buildscript {
    ext {
        springBootVersion = '1.5.2.RELEASE'
    }
    repositories {
        maven { url "https://artifactory.allstate.com/artifactory/libs-release" }
        mavenCentral()
        flatDir {
            dirs "$projectDir/lib/**/**"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.3')

    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'maven-publish'
apply plugin: 'org.sonarqube'

sourceCompatibility = 1.8

repositories {
    maven { url "https://artifactory.allstate.com/artifactory/libs-release" }
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Dalston.M1'
    }
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile("org.springframework.boot:spring-boot-starter-actuator")

    compile('com.allstate.ars.compozed:common-logging:0.0.10')
    compile('com.allstate.ars.compozed:common-utils:0.0.13')
    compile('com.allstate.ars.compozed:common-exception:0.0.11')

    compile('org.apache.velocity:velocity:1.7')
    compile('org.mybatis.spring.boot:mybatis-spring-boot-starter:1.3.0')

    compile('org.apache.tomcat.embed:tomcat-embed-jasper')
    compile('org.eclipse.jdt.core.compiler:ecj:4.6.1')
    compile('org.webjars:bootstrap:3.3.7')

    def SPRINGFOX_VERSION = "2.6.1"
    compile("io.springfox:springfox-swagger2:" + SPRINGFOX_VERSION)
    compile("io.springfox:springfox-swagger-ui:" + SPRINGFOX_VERSION)

    def CUCUMBER_VERSION = "1.2.4"
    testCompile("info.cukes:cucumber-java:" + CUCUMBER_VERSION)
    testCompile("info.cukes:cucumber-junit:" + CUCUMBER_VERSION)
    testCompile("info.cukes:cucumber-spring:" + CUCUMBER_VERSION)

    compile("org.springframework.cloud:spring-cloud-starter-sleuth")

    compile('com.oracle:ojdbc6:11.2.0.3')
//    runtime('mysql:mysql-connector-java')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    compile('com.allstate.compozed:NautilusStarter:1.0.2')
    compile('org.projectlombok:lombok')
}

if (System.env.BUILD_NUMBER) {
    version = "0.0.${System.env.BUILD_NUMBER}"
}
else {
    version = "0.0"
}

def artifactName
if (System.env.ARTIFACT_NAME) {
    artifactName = System.env.ARTIFACT_NAME
} else {
    artifactName = 'prototype-service-local'
}

jar {
    baseName artifactName
    version version
}

task zipTestResults(type: Zip) {
    from projectDir
    appendix 'audit'
    include 'build/reports/'
    include 'build/test-results'
    include 'git-commit'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.allSource
    baseName artifactName
    appendix 'sources'
}

publishing {
    publications {
        demo(MavenPublication) {
            groupId 'com.allstate.ars.compozed'
            artifactId artifactName
            version version

            from components.java

            artifact(zipTestResults) {
                classifier "audit"
            }
        }
    }

    repositories {
        maven {
            name 'artifactory'
            url "https://artifactory.allstate.com/artifactory/libs-release-local"
            credentials {
                username = System.env.USERNAME
                password = System.env.PASSWORD
            }
        }
    }
    publish.dependsOn build
}

ext {
    jacocoExclusions = ['**/libs/*',
                        '**/com/allstate/ars/compozed/constant**',
                        '**/com/allstate/ars/compozed/exception**',
                        '**/com/allstate/ars/compozed/domain/response/ErrorResponse.class',
                        '**/com/allstate/ars/compozed/domain/response/AbstractResponse.class',
                        '**/com/allstate/ars/compozed/domain/ExampleRequestResponse.class',
                        "**/com/allstate/ars/compozed/ExampleServiceApplication.class"]
    sonarExclusions = "**/libs/*," +
            "**/com/allstate/ars/compozed/constant**," +
            "**/com/allstate/ars/compozed/exception**," +
            "**/com/allstate/ars/compozed/domain/response/AbstractResponse.**," +
            "**/com/allstate/ars/compozed/domain/ExampleRequestResponse.**," +
            "**/com/allstate/ars/compozed/ExampleServiceApplication.**"

}

apply from: "$projectDir/gradle/codeCoverage.gradle"
apply from: "$projectDir/gradle/codeQuality.gradle"
